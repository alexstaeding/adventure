import org.jetbrains.dokka.gradle.DokkaTask

plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.4.0'
  id 'org.jetbrains.dokka' version '1.4.0-rc'
}

sourceSets {
  examples {
    compileClasspath += main.compileClasspath
    runtimeClasspath += main.runtimeClasspath
  }
}

dependencies {
  api project(':adventure-api')
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
  testImplementation "org.jetbrains.kotlin:kotlin-test-junit5"
  examplesImplementation sourceSets.main.output
}

// Docs
// TODO once https://github.com/Kotlin/dokka/issues/1339, add Adventure logo

javadocJar {
  from dokkaHtml
}

tasks.withType(DokkaTask).configureEach {
  dokkaSourceSets {
    main {
      includes = [ layout.projectDirectory.file("src/main/dokka/descriptions.md").toString()]
      samples = sourceSets.examples.kotlin.sourceDirectories.files.collect { it.toString() }
      /*externalDocumentationLink { // TODO: javadoc publication
        url = new URL("https://jd.adventure.kyori.net")
      }*/
    }
  }
}

jar {
  manifest.attributes(
    'Automatic-Module-Name': 'net.kyori.adventure.extension.kotlin'
  )
}

kotlin {
  explicitApi()
}

compileKotlin {
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

compileExamplesKotlin {
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

compileTestKotlin {
  kotlinOptions {
    jvmTarget = '1.8'
  }
}
